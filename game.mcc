# VENT PARTICLES
{"type":"repeating", "auto":true}
/execute @e[name=lobbyvent] ~ ~ ~ particle bubble ~1.5 ~1 ~1.5 1 10 1 0 200		
	{"type":"chain"}
	/execute @e[tag=largeVent] ~ ~ ~ particle bubble ~1.5 ~1 ~1.5 1 10 1 0 200
	/execute @e[tag=smallVent] ~ ~ ~ particle bubble ~0.5 ~1 ~0.5 0.5 5 0.5 0 200
	

--- Effects Score ---
0 = Default: Slow falling effect - buggy, move on to #1
1 = AEC Slow falling effect
2 = Rising levitation - above vent entity
3 = Free falling, no levitation.
	
# EFFECTS
{"type":"repeating", "auto":true}
	-- Default score of 1
	/scoreboard players set @a effects 0
	{"type":"chain"}

		-- Count ticks on effect = 0
		/scoreboard players add @a[m=adventure,score_effects_min=0,score_effects=0] effectsTimer 1

		-- Move on to 1 after 3 ticks
		/scoreboard players set @a[m=adventure,score_effectsTimer_min=4] effects 1
		
		-- Apply 2 if above a vent
		/execute @e[tag=largeVent] ~ ~ ~ execute @a[dx=3,dy=51,dz=3] ~ ~ ~ scoreboard players set @p effects 2
		/execute @e[tag=smallVent] ~ ~ ~ execute @a[dx=1,dy=25,dz=1] ~ ~ ~ scoreboard players set @p effects 2
		/execute @e[name=lobbyvent] ~ ~ ~ execute @a[dx=3,dy=10,dz=3] ~ ~ ~ scoreboard players set @p effects 2

		-- Set to 0 if player has taken damage (and reduce damage score)
		/scoreboard players set @a[score_damage_min=1] effects 3
		
		-- Reduce damage score so that falling stops when gone
		/scoreboard players remove @a[score_damage_min=1] damage 3

		-- Set effectsTimer if currently Rising or Free Falling.
		/scoreboard players set @a[m=adventure,score_effects_min=2,score_effects=3] effectsTimer 0

		-- Clear effects when appropriate - Use "protectEffects" to prevent AEC slowing from being removed
			
			-- Remove protectEffects tag if no longer applies
			/scoreboard players tag @a[m=adventure,score_effects_min=0,score_effects=0,tag=protectEffects] remove protectEffects
			/scoreboard players tag @a[m=adventure,score_effects_min=2,score_effects=2,tag=protectEffects] remove protectEffects
			/scoreboard players tag @a[m=adventure,score_effects_min=3,score_effects=3,tag=protectEffects] remove protectEffects

			-- Clear all effects unless the players is tagged as "protectEffects"
			/effect @a[m=adventure,tag=!protectEffects] clear

		-- Apply effects based on score
			
			--  ALL: Jump boost to avoid fall damage
			/effect @a[m=adventure,score_effects_min=0,score_effects=3] minecraft:jump_boost 1 254 true

			-- 0: Default: Slow falling effect - buggy, move on to #1 soon
			/effect @a[m=adventure,score_effects_min=0,score_effects=0] minecraft:levitation 1 1 true
			--# /execute @a[m=adventure,score_effects_min=0,score_effects=0] ~ ~ ~ say Applying buggy falling effect
			
			-- 1: For "slow falling", use a matrix of AECs to apply -11 levitation for 5 minutes (600 ticks).
			{"executeAs":"@a[m=adventure,score_effects_min=1,score_effects=1,tag=!protectEffects]"}
				/effect @p clear
				/summon AreaEffectCloud ~ ~1.0 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				/summon AreaEffectCloud ~ ~0.5 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				/summon AreaEffectCloud ~ ~0.0 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				/summon AreaEffectCloud ~ ~-0.5 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				/summon AreaEffectCloud ~ ~-1.0 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				/summon AreaEffectCloud ~ ~-1.5 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				/summon AreaEffectCloud ~ ~-2.0 ~ {Radius:1.5f,Particle:take,Age:0,Duration:20,Effects:[{Id:25,Amplifier:-11,Duration:6000,ShowParticles:0b}]}
				--# /say Applying AEC falling effect
				/scoreboard players tag @p add protectEffects
			{"executeAs":""}
			
			-- 2: For Rising apply levitation 8.
			/effect @a[m=adventure,score_effects_min=2,score_effects=2] minecraft:levitation 1 8 true
			--# /execute @a[m=adventure,score_effects_min=2,score_effects=2] ~ ~ ~ say Applying rise effect

 --- In Game ---
 0 = Spawn into the game
 1 = Yes playing
 2 = Failed to stay up, kill him
 3 = Died, loose a life, then...
     If "lives" greater than 0 reenter game, otherwise leave them out and announce it.
	 
# TRIGGER NEW GAME
{"type":"repeating", "auto":true, "conditional":false}
	{"executeAs" : "@a[score_trigger_start_min=1]"}
		-- Call the StartCountdown event, and set trigger_start to 0
		!start_event StartCountdown
		{"type":"chain"}
		/scoreboard players set @p trigger_start 0
	{"executeAs" : ""}
	/scoreboard players enable @a trigger_start
	
# START COUNTDOWN
!event StartCountdown
	{"type":"chain", "auto":true, "conditional":false}
	/title @a times 0 40 20
	/scoreboard players set @e[type=ArmorStand,name=SYSTEM,score_countdown=0] countdown 60
	!start_loop Countdown
		
# COUNTDOWN
!loop Countdown
	{"type":"chain", "auto":true, "conditional":false}		
	{"executeAs":"@e[name=SYSTEM,score_countdown=60,score_countdown_min=60]"}
		/title @a reset
		/title @a title {"text":"3","color":"white","bold":"true"}
		
	{"executeAs":"@e[name=SYSTEM,score_countdown=40,score_countdown_min=40]"}
		/title @a title {"text":"2","color":"white","bold":"true"}
		
	{"executeAs":"@e[name=SYSTEM,score_countdown=20,score_countdown_min=20]"}
		/title @a title {"text":"1","color":"white","bold":"true"}
		
	{"executeAs":"@e[name=SYSTEM,score_countdown=0,score_countdown_min=0]"}
		/title @a title {"text":"Go!","color":"green","bold":"true"}
		!stop_loop Countdown
			{"conditional":true}
				!start_event StartNewGame
	{"executeAs":"", "conditional":false}
	/scoreboard players remove @e[type=ArmorStand,name=SYSTEM] countdown 1
	
# START NEW GAME
!event StartNewGame
	{"type":"chain", "auto":true, "conditional":false}
	-- Reset player gamemode, includes those spectating
	/gamemode adventure @a[m=!creative]
	-- Set lives and mark players "ingame" 0 to spawn them
	/scoreboard objectives setdisplay sidebar lives
	/scoreboard players set @a[m=adventure] lives 0
	/scoreboard players operation @a[m=adventure] lives = $lives vars
	/scoreboard players set @a[m=adventure] killed 0
	/scoreboard players set @a[m=adventure] ingame 0
	-- Clone SPECTATE wall
	/clone -26 2 -40 -23 3 -39 -26 5 -50
	-- Blank SETTINGS wall
	/clone -26 2 -74 -23 3 -73 -26 5 -64
	-- Start the timer
	!start_event StartTimer
	-- Play music
	/execute @e[name=SOUNDPLAYER] ~ ~ ~ playsound minecraft:record.far master @a[m=adventure] ~ ~ ~ 100 1 1

# SPAWN PLAYERS
{"type":"repeating", "auto":true, "conditional":false}	
{"executeAs":"@a[m=adventure,score_ingame_min=0,score_ingame=0]"}
	/clear @p 
	{"type":"chain"}
	/give @p minecraft:stone_sword
	/give @p minecraft:bow
	/give @p minecraft:arrow 64
	/give @p minecraft:snowball 16 0 {display:{Name:Fireball}}
	/give @p tipped_arrow 16 0 {Potion:"minecraft:fire_resistance",display:{Name:Explosive Arrow}}
	/tp @p @r[type=ArmorStand,name=startingPoint]
	/scoreboard players set @p ingame 1
{"executeAs":""}
	
# DEATH HANDLING
{"type":"repeating", "auto":true}
/scoreboard players set @a[m=adventure,score_ingame_min=1,score_ingame=1] ingame 2 {OnGround:true}
	{"type":"chain"}
	/execute @a[m=adventure,score_ingame_min=1,score_ingame=1] ~ ~ ~ detect ~ ~-0.5 ~ minecraft:lava 0 scoreboard players set @p ingame 2
	/kill @a[m=adventure,score_ingame_min=2,score_ingame=2,score_killed=0]
	-- Handle player death
	{"executeAs":"@a[m=adventure,score_killed_min=1,score_timeSinceDeath_min=1]"}
		/scoreboard players set @p ingame 3
		/scoreboard players remove @p lives 1
		/scoreboard players set @p damage 0
		/scoreboard players set @p killed 0
	{"executeAs":""}
	-- Announce if the player is out and reset scoreboards
	{"executeAs":"@a[m=adventure,score_ingame_min=3,score_ingame=3,score_lives=0]"}
		/tellraw @a ["",{"selector":"@p"},{"text":" is out!"}]
		/scoreboard players reset @p ingame
		/scoreboard players reset @p lives
	{"executeAs":""}
	-- Respawn if player has lives
	/scoreboard players set @a[m=adventure,score_ingame_min=3,score_ingame=3,score_lives_min=1] ingame 0
	
# SPECTATE
{"type":"repeating", "auto":true, "conditional":false}
	{"executeAs" : "@a[score_trigger_spectate_min=1]"}
		-- Set the spectator mode, teleport to random startingPoint and set trigger_spectate to 0
		/gamemode spectator @p
		{"type":"chain"}
		/tp @p @r[type=ArmorStand,name=startingPoint]
		/scoreboard players set @p trigger_spectate 0
	{"executeAs":""}
	/scoreboard players enable @a trigger_spectate

# END DETECT
{"type":"repeating", "auto":true, "conditional":false}
/scoreboard players set @e[name=SYSTEM] testforOutput -1
	{"type":"chain"}
	/stats block ~1 ~ ~ set SuccessCount @e[name=SYSTEM] testforOutput
	/testfor @a[m=adventure,score_lives_min=1]
	-- Above works assuming testfor block will be 1 in x direction of stats block.
	{"executeAs":"@e[name=SYSTEM,score_testforOutput=1,score_testforOutput_min=1]"}
		!start_event EndGame
	{"executeAs":""}
	/scoreboard players set @e[name=SYSTEM] testforOutput -1
	
# END GAME
!event EndGame
	{"type":"chain", "auto":true, "conditional":false}
	-- Announce winner
	{"executeAs":"@a[m=adventure,score_lives_min=1,c=1]"}
		/tellraw @a ["",{"selector":"@p","color":"green","bold":"true"},{"text":" has won! ","color":"green","bold":"true"}]
		/title @a title ["",{"selector":"@p","color":"green","bold":"true"},{"text":" has won! ","color":"green","bold":"true"}]
	{"executeAs":""}
	-- Move everyone back to the lobby
	/gamemode adventure @a[m=spectator]
	/clear @a[m=adventure]
	/effect @a[m=adventure] minecraft:saturation 1 100 true
	/effect @a[m=adventure] minecraft:instant_health 1 100 true
	/tp @a[m=adventure] -24 6 -56 0 0
	/scoreboard players reset @a[m=adventure] ingame
	/scoreboard players reset @a[m=adventure] lives
	/scoreboard objectives setdisplay sidebar
	--- Stop the music
	/stopsound @a[m=adventure]
	--- Kill all mobs
	/kill @e[type=Shulker]
	/kill @e[type=Ghast]
	/kill @e[type=Blaze]
	--- Kill any left over Fireballs
	/kill @e[type=Fireball]
	-- Clone START GAME wall
	/clone -26 2 -45 -23 3 -44 -26 5 -50
	-- Clone SETTINGS wall
	/clone -26 2 -70 -23 3 -69 -26 5 -64
	-- Stop the timer
	!start_event StopTimer




	
	
	
	
	


	



